plugins {
    id 'java-library'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

group 'co.insecurity'
scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    versionIncrementer 'incrementPatch'
    branchVersionIncrementer = [
            'feature/.*': 'incrementMinor',
            'bugfix/.*': 'incrementPatch',
    ]
    createReleaseCommit false
    hooks {
        pre 'fileUpdate', [
                files: ['README.md'],
                pattern: {v, c -> /\b$v\b/},
                replacement: {v, c -> "$v"},
        ]
        pre 'commit'
    }
}
project.version = scmVersion.version
logger.info("version: ${project.version}")

apply from: "${rootDir}/gradle/maven-publish.gradle"

repositories {
    mavenCentral()
    jcenter()
    // mavenLocal()
}

dependencies {
    api 'com.baqend:bloom-filter:1.2.3'

    implementation 'org.slf4j:slf4j-api:1.7.10'
    runtimeOnly 'org.slf4j:slf4j-log4j12:1.7.10'

    testImplementation 'junit:junit:4.11'
}

// task sourceJar(type: Jar) {
//     from sourceSets.main.allJava
// }

